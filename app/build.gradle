buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        //classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'io.fabric'
//apply plugin: 'com.jakewharton.hugo'

static def gitBranch() {
    def branch = 'GitHub'
    try {
        def gitcheck = 'command -v git >/dev/null 2>&1'.execute()
        gitcheck.waitFor()
        if (gitcheck.exitValue() == 0) {
            def proc = 'git rev-parse --abbrev-ref HEAD'.execute()
            proc.in.eachLine { line -> branch = line }
            proc.err.eachLine { line -> println line }
            proc.waitFor()
        }
    } catch (Exception e) {
        // Do nothing
        println e
    }
    branch
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.zaen.testly"
        versionCode 1
        versionName "1.0"

        minSdkVersion 21
        targetSdkVersion 27

        renderscriptTargetApi 27
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

ext {
    supportLibVersion = '27.0.2'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Google Android & Support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibVersion"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    // Material Dialog
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.afollestad:material-cab:0.1.12'
    testImplementation 'junit:junit:4.12'

    // Android About Page
    implementation 'com.github.medyo:android-about-page:1.2.2'

    // App Theme Helper (kabouzeid fork)
    implementation('com.github.kabouzeid:app-theme-helper:1.3.7@aar') {
        transitive = true
    }

    // utils
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true;
    }

    // Iconics
    implementation "com.mikepenz:iconics-core:3.0.2@aar"
    implementation "com.mikepenz:iconics-views:3.0.2@aar"
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'
    implementation 'com.mikepenz:fontawesome-typeface:5.0.6.0@aar'
    implementation 'com.mikepenz:community-material-typeface:2.0.46.1@aar'

    // Base recycler View Adapter Helper
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.21'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '27.0.2'
            }
        }
    }
}
